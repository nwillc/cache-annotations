/*
 * Copyright (c) 2015, nwillc@gmail.com
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

buildscript {
    repositories {
        mavenLocal()
        jcenter {
            url "http://jcenter.bintray.com"
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.nwillc:vplugin:1.2.4",
                "nl.eveoh:gradle-aspectj:1.6",
                "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4",
                'au.com.ish.gradle:release:2.2.2',
                'org.tmatesoft.sqljet:sqljet:1.1.9'
    }
}

project.ext {
    aspectjVersion = "1.8.7"
}

apply plugin: "aspectj"
apply plugin: "java"
apply plugin: "com.github.nwillc.vplugin"
apply plugin: "release"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven"

repositories {
    jcenter {
        url "http://jcenter.bintray.com"
    }
}

dependencies {
    compile "org.slf4j:slf4j-api:1.7.13",
            "org.aspectj:aspectjrt:1.8.7",
            "javax.cache:cache-api:1.0.0",
            'com.github.nwillc:simplecache:1.0.0'

    testCompile "junit:junit:4.12",
            'org.assertj:assertj-core:3.2.0',
            'org.mockito:mockito-core:2.0.31-beta'

}

group = "com.github.nwillc"
version = "0.1.2"
targetCompatibility = "1.8"
sourceCompatibility = "1.8"

test {
    beforeTest { descriptor ->
        logger.lifecycle("\tRunning " + descriptor.className + "." + descriptor.name)
    }

    afterSuite { descriptor, result ->
        if (descriptor.parent == null) {
            logger.lifecycle("\tTests run: " + result.testCount
                    + ", Failures: " + result.failedTestCount
                    + ", Skipped: " + result.skippedTestCount)
        }
    }

    testLogging {
        exceptionFormat "full"
    }
}

jar {
    dependsOn classes
    from sourceSets.main.output
    exclude("**/examples")     // Exclude the examples needed for tests
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    exclude("**/examples")     // Exclude the examples needed for tests
    classifier = "javadoc"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}

release {
    failOnSnapshotDependencies = true
    allowLocalModifications = true
    releaseDryRun = false
    scm = "git"
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    dryRun = false
    publish = true
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'JSR 107 caching annotations'
        websiteUrl = 'https://github.com/nwillc/cache-annotations'
        issueTrackerUrl = 'https://github.com/nwillc/cache-annotations/issues'
        vcsUrl = 'https://github.com/nwillc/cache-annotations.git'
        licenses = ['ISC']
        labels = ['cach-annotations']
        publicDownloadNumbers = true
    }
}